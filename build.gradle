buildscript {
    ext.kotlin_version = '1.3.72'

    repositories {
        mavenCentral()
        jcenter()
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
    }
}
plugins {
    id "com.jfrog.bintray" version "1.8.4"
	id "com.github.ben-manes.versions" version "0.20.0"
}

def artifactVersion = '1.1.0'
def artifactGroup = 'com.chimerapps.gradle'
group "$artifactGroup"
version "$artifactVersion"

apply plugin: 'java'
apply plugin: 'kotlin'
apply plugin: 'kotlin-kapt'
apply plugin: 'java-gradle-plugin'
apply plugin: 'maven-publish'
apply plugin: 'idea'

sourceCompatibility = 1.8
targetCompatibility = 1.8

task sourceJar(type: Jar) {
    from sourceSets.main.allSource
}

task javadocJar(type: Jar) {
    classifier = 'javadoc'
    from 'docs/README.txt'
}

repositories {
    jcenter()
    mavenCentral()
}

dependencies {
    compile "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
    compile "org.jetbrains.kotlin:kotlin-reflect:$kotlin_version"

    compile 'com.squareup.retrofit2:retrofit:2.9.0'
    compile 'com.squareup.okhttp3:okhttp:4.7.2'
    compile 'com.squareup.moshi:moshi:1.9.3'
    compile 'com.squareup.retrofit2:converter-moshi:2.9.0'

    compile 'com.chimerapps:moshi-generator-runtime:0.11.0'
    kapt 'com.chimerapps:moshi-generator:0.11.0'

    testCompile 'junit:junit:4.12'
}

bintray {
    user = System.getenv('BINTRAY_USER')
    key = System.getenv('BINTRAY_KEY')
    pkg {
        repo = 'maven'
        name = 'icapps-translations-gradle-plugin'
        licenses = ['Apache-2.0']
        vcsUrl = 'https://github.com/Chimerapps/icapps-translations-gradle-plugin'

        version {
            name = "$artifactVersion"
            desc = "Gradle plugin for icapps_translations"
            released = new Date()
        }
    }
    publications = ['maven']
}

def pomConfig = {
    licenses {
        license {
            name "The Apache Software License, Version 2.0"
            url "http://www.apache.org/licenses/LICENSE-2.0.txt"
            distribution "repo"
        }
    }
    developers {
        developer {
            name "Nicola Verbeeck"
            email "nicola@chimerapps.com"
            organization "Chimerapps"
        }
    }
}

publishing {
    publications {
        maven(MavenPublication) {
            groupId "$artifactGroup"
            artifactId 'icapps-translations-plugin'
            version "$artifactVersion"

            from components.java

            artifact sourceJar {
                classifier "sources"
            }
            artifact javadocJar

            pom.withXml {
                def root = asNode()
                root.appendNode('description', 'Gradle plugin that allows translations to be downloaded from icapps translations')
                root.appendNode('name', 'icapps-translations-plugin')
                root.appendNode('url', 'https://github.com/Chimerapps/icapps-translations-gradle-plugin')
                root.children().last() + pomConfig
            }
        }
    }
}
